plugins {
    id 'java'
    alias libs.plugins.springboot
    alias libs.plugins.springDependencyManagement
}

// Integration tests live under src/test (repo convention); we separate them via JUnit tags + a dedicated Gradle task.
dependencies {
    implementation project(path: ':all')
    implementation project(path: ':core')
    implementation project(path: ':limits')
    implementation project(path: ':subscriptions')

    implementation 'org.springframework.boot:spring-boot-starter-actuator'
    implementation 'org.springframework.boot:spring-boot-starter-webmvc'
    implementation 'org.springframework.boot:spring-boot-starter-data-jdbc'
    implementation 'org.springframework.boot:spring-boot-starter-security'

    implementation 'org.springframework.boot:spring-boot-starter-flyway'
    implementation 'org.flywaydb:flyway-database-postgresql'

    implementation 'com.h2database:h2'
    implementation libs.postgresql

    testImplementation 'org.springframework.boot:spring-boot-starter-webmvc-test'
    testImplementation 'org.springframework.security:spring-security-test'
    testImplementation 'org.springframework.boot:spring-boot-starter-flyway-test'
    testImplementation project(path: ':remoteclient')
    testImplementation libs.testcontainers
    testImplementation libs.testcontainers.junitJuniper
    testRuntimeOnly 'org.junit.platform:junit-platform-launcher'
}

test {
    useJUnitPlatform()
    // Keep docker/compose tests out of the default unit test run.
    useJUnitPlatform {
        excludeTags 'docker'
    }
}

tasks.register('dockerComposeIntegrationTest', Test) {
    description = 'Runs docker/compose integration tests.'
    group = 'verification'
    testClassesDirs = sourceSets.test.output.classesDirs
    classpath = sourceSets.test.runtimeClasspath
    useJUnitPlatform {
        includeTags 'docker'
    }
}
