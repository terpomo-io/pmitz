plugins {
    id 'java'
    alias libs.plugins.springboot
    alias libs.plugins.springDependencyManagement
}

sourceSets {
    integrationTest {
        java.srcDir file('src/integrationTest/java')
        resources.srcDir file('src/integrationTest/resources')
        compileClasspath += sourceSets.main.output + configurations.testRuntimeClasspath
        runtimeClasspath += output + compileClasspath
    }
}

configurations {
    integrationTestImplementation.extendsFrom testImplementation
    integrationTestRuntimeOnly.extendsFrom testRuntimeOnly
}

dependencies {
    implementation project(path: ':all')
    implementation project(path: ':core')
    implementation project(path: ':limits')
    implementation project(path: ':subscriptions')

    implementation 'org.springframework.boot:spring-boot-starter-actuator'
    implementation 'org.springframework.boot:spring-boot-starter-webmvc'
    implementation 'org.springframework.boot:spring-boot-starter-data-jdbc'
    implementation 'org.springframework.boot:spring-boot-starter-security'

    implementation 'org.springframework.boot:spring-boot-starter-flyway'
    implementation 'org.flywaydb:flyway-database-postgresql'

    implementation 'com.h2database:h2'
    implementation libs.postgresql

    testImplementation 'org.springframework.boot:spring-boot-starter-webmvc-test'
    testImplementation 'org.springframework.security:spring-security-test'
    testImplementation 'org.springframework.boot:spring-boot-starter-flyway-test'
    testRuntimeOnly 'org.junit.platform:junit-platform-launcher'

    integrationTestImplementation project(path: ':remoteclient')
    integrationTestImplementation libs.testcontainers
    integrationTestImplementation libs.testcontainers.junitJuniper
}

test {
    useJUnitPlatform()
}

tasks.register('integrationTest', Test) {
    description = 'Runs docker/compose integration tests.'
    group = 'verification'
    testClassesDirs = sourceSets.integrationTest.output.classesDirs
    classpath = sourceSets.integrationTest.runtimeClasspath
    useJUnitPlatform()
}
