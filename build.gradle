apply plugin: 'java'

group 'io.terpomo.pmitz'
version '1.0.0-SNAPSHOT'

subprojects {
    apply plugin: 'java'
    apply plugin: 'checkstyle'

    checkstyle {
        toolVersion = "10.15.0"
        ignoreFailures = false
        showViolations = true
        configFile = rootProject.file('./checkstyle/config/pmitz-checkstyle.xml')
        configProperties = [
                'headerType': 'apache2',
                'headerCopyrightPattern': '20\\d\\d(-20\\d\\d)?',
                'projectRootPackage': 'io.terpomo',
                'avoidStaticImportExcludes': "org.assertj.core.api.AssertionsForClassTypes.*"
        ]
    }

    dependencies {
        checkstyle libs.spring.javaformat

        testImplementation libs.junit.jupiter
        testImplementation libs.mockito.junit.jupiter
        testImplementation libs.assertj.core
    }

    configurations.checkstyle {
        resolutionStrategy.capabilitiesResolution.withCapability("com.google.collections:google-collections") {
            select("com.google.guava:guava:0")
        }
    }

    tasks.withType(Jar).configureEach {
        archiveBaseName.set("pmitz-${project.name}")
        archiveVersion.set(rootProject.version.toString())
        archiveFileName.set("${archiveBaseName.get()}-${archiveVersion.get()}.jar")
    }

    repositories {
        mavenCentral()
    }

    test {
        useJUnitPlatform()
    }
}