plugins {
    id 'java'
    alias(libs.plugins.benManesVersions)
}

group = 'io.terpomo.pmitz'
version = '0.8.0-SNAPSHOT'

subprojects {
    apply plugin: 'checkstyle'
    apply plugin: 'com.github.ben-manes.versions'

    tasks.withType(JavaCompile).configureEach {
        sourceCompatibility = '17'
        targetCompatibility = '17'
        options.encoding = 'UTF-8'
    }

    java {
        toolchain {
            languageVersion = JavaLanguageVersion.of(17)
        }
    }

    checkstyle {
        toolVersion = "11.0.0"
        ignoreFailures = false
        showViolations = true
        configFile = rootProject.file('./checkstyle/config/pmitz-checkstyle.xml')
        configProperties = [
                'headerType': 'apache2',
                'headerCopyrightPattern': '20\\d\\d(-20\\d\\d)?',
                'projectRootPackage': 'io.terpomo',
                'avoidStaticImportExcludes': "org.assertj.core.api.AssertionsForClassTypes.*,com.github.tomakehurst.wiremock.client.WireMock.*"
        ]
    }

    dependencies {
        checkstyle libs.springJavaformatCheckstyle
    }

    configurations.checkstyle {
        resolutionStrategy.capabilitiesResolution.withCapability("com.google.collections:google-collections") {
            select("com.google.guava:guava:0")
        }
    }

    tasks.withType(Jar).configureEach {
        archiveBaseName.set("pmitz-${project.name}")
        archiveVersion.set(rootProject.version.toString())
        archiveFileName.set("${archiveBaseName.get()}-${archiveVersion.get()}.jar")
    }

    repositories {
        mavenCentral()
    }
}
